<!DOCTYPE html>
<html>
<head>
    <meta name='google-site-verification' content='REYQ1_I6HGowAE7LOLVqfQbnKaB4IfFpMlGzMbJj55Q' />
    <meta name='viewport' content='width=device-width, initial-scale=1' />
    <link rel='icon' href='../img/micronaut-favicon.ico' />
    <meta charset='UTF-8' />
    <title>Send Emails from Micronaut | Micronaut Guides | Micronaut Framework</title>
    <meta name='description' content='Send Emails from Micronaut. Learn how to send emails with AWS SES and SendGrid from a Micronaut app and leverage @Requires annotation to load beans conditionally.' />
    <link rel="stylesheet" href="../css/highlight/agate.css">
    <script src="../js/highlight.pack.js"></script>
    <script>hljs.initHighlightingOnLoad();</script>
    <link rel='stylesheet' href='../css/main.css' />
    <link rel='stylesheet' href='../css/screen.css' />
    <script src='https://cdnjs.cloudflare.com/ajax/libs/clipboard.js/1.7.1/clipboard.min.js'></script>
</head>
<body class='guide'>
<div id="navigation">
    <div class="navTitle">
        <span id="logo"><a href="http://micronaut.io" title="Go to Micronaut Website"><img src="../img/micronaut-logo-white.svg" alt="Micronaut"/></a></span>
    </div>
    <div class="navLinks">
        <ul>
            <li>
                <a href="http://guides.micronaut.io">Micronaut Guides</a>
                <span> &rarr; </span>
                <a href="http://guides.micronaut.io/micronaut-email-groovy/guide/index.html">Send Emails from Micronaut</a>
            </li>
        </ul>

    </div>
</div>

<div class='fullwidth'><article class="guide">
    <aside>
        <a href="https://travis-ci.org/micronaut-guides/micronaut-email-groovy"><img src="https://travis-ci.org/micronaut-guides/micronaut-email-groovy.svg?branch=master"></a>

        <button type="button" class="btn btn-large" onclick="window.location.href=&quot;https://github.com/micronaut-guides/micronaut-email-groovy&quot;"><img height="15" src="../img/github_white.svg" alt="Github"/><span>&nbsp;Get the Code</span></button>


        <ul class="githublinks">
            <li><button type="button" class="btn btn-default" href="#" onclick="document.getElementById('github-url').value='git@github.com:micronaut-guides/micronaut-email-groovy.git'" class="codeButton">SSH</button></li>
            <li><button type="button" class="btn btn-default" onclick="document.getElementById('github-url').value='https://github.com/micronaut-guides/micronaut-email-groovy.git'" class="codeButton">HTTPS</button></li>
        </ul>
        <div class="input-group">
            <!-- Target -->
            <input id="github-url" value="git@github.com:micronaut-guides/micronaut-email-groovy.git" type="text" />
            <!-- Trigger -->
            <button type="button" class="clippy btn btn-default" type="button" data-clipboard-target="#github-url">
                <img width="13" src="../img/clippy.svg" alt="Copy to clipboard">
            </button>
        </div>
        <script>var clipboard = new Clipboard('.clippy');</script>
        <button type="button" class="btn btn-large" onclick="window.location.href=&quot;https://github.com/micronaut-guides/micronaut-email-groovy/archive/master.zip&quot;">Download ZIP</button>
        
        <div id="table-of-content">
            <h3>Table of Contents</h3>
            
            <div class="toc-item" style="margin-left:0px"><a href="#gettingStarted"><strong>1</strong><span>Getting Started</span></a></div>
            
            <div class="toc-item" style="margin-left:10px"><a href="#requirements"><strong>1.1</strong><span>What you will need</span></a></div>
            
            <div class="toc-item" style="margin-left:10px"><a href="#solution"><strong>1.2</strong><span>Solution</span></a></div>
            
            <div class="toc-item" style="margin-left:0px"><a href="#application"><strong>2</strong><span>Application</span></a></div>
            
            <div class="toc-item" style="margin-left:0px"><a href="#writingTheApp"><strong>3</strong><span>Writing the App</span></a></div>
            
            <div class="toc-item" style="margin-left:10px"><a href="#controller"><strong>3.1</strong><span>Controller</span></a></div>
            
            <div class="toc-item" style="margin-left:10px"><a href="#services"><strong>3.2</strong><span>Email Service</span></a></div>
            
            <div class="toc-item" style="margin-left:20px"><a href="#awsSesService"><strong>3.2.1</strong><span>AWS SES</span></a></div>
            
            <div class="toc-item" style="margin-left:20px"><a href="#sendGridService"><strong>3.2.2</strong><span>SendGrid</span></a></div>
            
            <div class="toc-item" style="margin-left:10px"><a href="#resources"><strong>3.3</strong><span>Run the app</span></a></div>
            
            <div class="toc-item" style="margin-left:10px"><a href="#test"><strong>3.4</strong><span>Test</span></a></div>
            
            <div class="toc-item" style="margin-left:10px"><a href="#validation"><strong>3.5</strong><span>Validation</span></a></div>
            
            <div class="toc-item" style="margin-left:0px"><a href="#testingTheApp"><strong>4</strong><span>Testing the Application</span></a></div>
            
            <div style="clear:both" ></div>
        </div>
        
    </aside>
    <header>
        <h1>Send Emails from Micronaut</h1>
        <p>Learn how to send emails with AWS SES and SendGrid from a Micronaut app and leverage @Requires annotation to load beans conditionally.</p>
        <p><strong>Authors:</strong> Sergio del Amo</p>
        <p><strong>Micronaut Version:</strong> 1.0.0.RC3</p>
    </header>
    

<h1 id="gettingStarted">1 Getting Started</h1>

<div class='contribute-btn'>
    <button type='button' class='btn btn-default' onclick='window.location.href="https://github.com/micronaut-guides/micronaut-email-groovy/edit/master/src/main/docs/guide/gettingStarted.adoc"'>
        <i class='fa fa-pencil-square-o'></i> Improve this doc
    </button>
</div>


<div class="paragraph">
<p>In this guide we are going to create a Micronaut app written in Groovy.</p>
</div>


<h2 id="requirements">1.1 What you will need</h2>

<div class='contribute-btn'>
    <button type='button' class='btn btn-default' onclick='window.location.href="https://github.com/micronaut-guides/micronaut-email-groovy/edit/master/src/main/docs/guide/requirements.adoc"'>
        <i class='fa fa-pencil-square-o'></i> Improve this doc
    </button>
</div>


<div class="paragraph">
<p>To complete this guide, you will need the following:</p>
</div>
<div class="ulist">
<ul>
<li>
<p>Some time on your hands</p>
</li>
<li>
<p>A decent text editor or IDE</p>
</li>
<li>
<p>JDK 1.8 or greater installed with <code>JAVA_HOME</code> configured appropriately</p>
</li>
</ul>
</div>


<h2 id="solution">1.2 Solution</h2>

<div class='contribute-btn'>
    <button type='button' class='btn btn-default' onclick='window.location.href="https://github.com/micronaut-guides/micronaut-email-groovy/edit/master/src/main/docs/guide/solution.adoc"'>
        <i class='fa fa-pencil-square-o'></i> Improve this doc
    </button>
</div>


<div class="paragraph">
<p>We recommend you to follow the instructions in the next sections and create the app step by step. However, you can go right to the <strong>completed example</strong>.</p>
</div>
<div class="ulist">
<ul>
<li>
<p><a href="https://github.com/micronaut-guides/micronaut-email-groovy/archive/master.zip">Download</a> and unzip the source</p>
</li>
</ul>
</div>
<div class="paragraph">
<p>or</p>
</div>
<div class="ulist">
<ul>
<li>
<p>Clone the <a href="https://git-scm.com/">Git</a> repository: <code>git clone <a href="https://github.com/micronaut-guides/micronaut-email-groovy.git" class="bare">https://github.com/micronaut-guides/micronaut-email-groovy.git</a></code></p>
</li>
</ul>
</div>
<div class="paragraph">
<p>Then, <code>cd</code> into the <code>complete</code> folder which you will find in the root project of the downloaded/cloned project.</p>
</div>


<h1 id="application">2 Application</h1>

<div class='contribute-btn'>
    <button type='button' class='btn btn-default' onclick='window.location.href="https://github.com/micronaut-guides/micronaut-email-groovy/edit/master/src/main/docs/guide/application.adoc"'>
        <i class='fa fa-pencil-square-o'></i> Improve this doc
    </button>
</div>


<div class="paragraph">
<p>Remove the <code>Application.java</code> created by the <code>mn create-app --features groovy</code> command.</p>
</div>
<div class="paragraph">
<p>Create a Groovy file instead which is used when running the application via Gradle or via deployment. You can also run the main class directly within your IDE if it is configured correctly.</p>
</div>
<div class="listingblock">
<div class="title">src/main/groovy/example/micronaut/Application.groovy</div>
<div class="content">
<pre class="highlightjs highlight"><code class="language-groovy" data-lang="groovy">package example.micronaut

import groovy.transform.CompileStatic
import io.micronaut.runtime.Micronaut

@CompileStatic
class Application {

    static void main(String[] args) {
        Micronaut.run(Application.class)
    }
}</code></pre>
</div>
</div>


<h1 id="writingTheApp">3 Writing the App</h1>

<div class='contribute-btn'>
    <button type='button' class='btn btn-default' onclick='window.location.href="https://github.com/micronaut-guides/micronaut-email-groovy/edit/master/src/main/docs/guide/writingTheApp.adoc"'>
        <i class='fa fa-pencil-square-o'></i> Improve this doc
    </button>
</div>


<div class="paragraph">
<p>Create a Groovy Micronaut app using the <a href="http://docs.micronaut.io/snapshot/guide/index.html#cli">Micronaut Command Line Interface</a>.</p>
</div>
<div class="paragraph">
<p><code>mn create-app example.micronaut.complete --features=groovy</code></p>
</div>
<div class="paragraph">
<p>The previous command createas a micronaut app with the default package <code>example.micronaut</code> in a folder named <code>complete</code>.</p>
</div>
<div class="paragraph">
<p>Due to the <code>--features groovy</code> flag, it generates a Groovy Micronaut app and it uses <a href="http://gradle.org">Gradle</a> build system. However, you could use
other build tool such as <code>Maven</code> or other programming languages such as <code>Java</code> or <code>Kotlin</code>.</p>
</div>


<h2 id="controller">3.1 Controller</h2>

<div class='contribute-btn'>
    <button type='button' class='btn btn-default' onclick='window.location.href="https://github.com/micronaut-guides/micronaut-email-groovy/edit/master/src/main/docs/guide/writingTheApp/controller.adoc"'>
        <i class='fa fa-pencil-square-o'></i> Improve this doc
    </button>
</div>


<div class="paragraph">
<p>Create <code>MailController</code> which use a collaborator, <code>emailService</code> to send and email.</p>
</div>
<div class="listingblock">
<div class="title">src/main/groovy/example/micronaut/MailController.groovy</div>
<div class="content">
<pre class="highlightjs highlight"><code class="language-groovy" data-lang="groovy">package example.micronaut

import groovy.transform.CompileStatic
import groovy.util.logging.Slf4j
import io.micronaut.http.HttpResponse
import io.micronaut.http.annotation.Body
import io.micronaut.http.annotation.Controller
import io.micronaut.http.annotation.Post
import io.micronaut.validation.Validated
import javax.validation.Valid

@Slf4j
@CompileStatic
@Controller('/mail') <i class="conum" data-value="1"></i><b>(1)</b>
@Validated <i class="conum" data-value="2"></i><b>(2)</b>
class MailController {

    EmailService emailService

    MailController( EmailService  emailService) { <i class="conum" data-value="3"></i><b>(3)</b>
        this.emailService = emailService
    }


    @Post('/send') <i class="conum" data-value="4"></i><b>(4)</b>
    HttpResponse send(@Body @Valid EmailCmd cmd) { <i class="conum" data-value="5"></i><b>(5)</b>
        log.info '{}', cmd.toString()
        emailService.send(cmd)
        HttpResponse.ok()  <i class="conum" data-value="6"></i><b>(6)</b>
    }
}</code></pre>
</div>
</div>
<div class="colist arabic">
<table>
<tr>
<td><i class="conum" data-value="1"></i><b>1</b></td>
<td>The class is defined as a controller with the <a href="http://docs.micronaut.io/snapshot/api/io/micronaut/http/annotation/Controller.html">@Controller</a> annotation mapped to the path /mail/send</td>
</tr>
<tr>
<td><i class="conum" data-value="2"></i><b>2</b></td>
<td>Add @Validated annotation at the class level to any class that requires validation.</td>
</tr>
<tr>
<td><i class="conum" data-value="3"></i><b>3</b></td>
<td>Constructor injection</td>
</tr>
<tr>
<td><i class="conum" data-value="4"></i><b>4</b></td>
<td>The <a href="http://docs.micronaut.io/snapshot/api/io/micronaut/http/annotation/Post.html">@Post</a> annotation is used to map the index method to all requests that use an HTTP POST</td>
</tr>
<tr>
<td><i class="conum" data-value="5"></i><b>5</b></td>
<td>Add <code>@Valid</code> to any method parameter which requires validation. Also use a POGO supplied as a JSON payload in the request to populate the email.</td>
</tr>
<tr>
<td><i class="conum" data-value="6"></i><b>6</b></td>
<td>Return 200 OK as the result</td>
</tr>
</table>
</div>
<div class="paragraph">
<p>The previous controller uses a POGO supplied in the request body as a JSON Payload</p>
</div>
<div class="listingblock">
<div class="title">src/main/groovy/example/micronaut/EmailCmd.groovy</div>
<div class="content">
<pre class="highlightjs highlight"><code class="language-groovy" data-lang="groovy">@ToString
@CompileStatic
class EmailCmd implements Email {

    @NotNull
    @NotBlank
    String recipient

    @NotNull
    @NotBlank
    String subject

    List&lt;String&gt; cc = []
    List&lt;String&gt; bcc = []
    String htmlBody
    String textBody
    String replyTo

}</code></pre>
</div>
</div>


<h2 id="services">3.2 Email Service</h2>

<div class='contribute-btn'>
    <button type='button' class='btn btn-default' onclick='window.location.href="https://github.com/micronaut-guides/micronaut-email-groovy/edit/master/src/main/docs/guide/writingTheApp/services.adoc"'>
        <i class='fa fa-pencil-square-o'></i> Improve this doc
    </button>
</div>


<div class="paragraph">
<p>Create an interface - <code>EmailService</code>. Any email provider present in the application should implement it.</p>
</div>
<div class="listingblock">
<div class="title">src/groovy/main/example/micronaut/EmailService.groovy</div>
<div class="content">
<pre class="highlightjs highlight"><code class="language-groovy" data-lang="groovy">package example.micronaut

import groovy.transform.CompileStatic

@CompileStatic
interface EmailService {
    void send(Email email)
}</code></pre>
</div>
</div>
<div class="listingblock">
<div class="title">src/groovy/main/example/micronaut/Email.groovy</div>
<div class="content">
<pre class="highlightjs highlight"><code class="language-groovy" data-lang="groovy">package example.micronaut

import groovy.transform.CompileStatic

@CompileStatic
interface Email {
    String getRecipient()
    List&lt;String&gt; getCc()
    List&lt;String&gt; getBcc()
    String getSubject()
    String getHtmlBody()
    String getTextBody()
    String getReplyTo()
}</code></pre>
</div>
</div>


<h2 id="awsSesService">3.2.1 AWS SES</h2>

<div class='contribute-btn'>
    <button type='button' class='btn btn-default' onclick='window.location.href="https://github.com/micronaut-guides/micronaut-email-groovy/edit/master/src/main/docs/guide/writingTheApp/services/awsSesService.adoc"'>
        <i class='fa fa-pencil-square-o'></i> Improve this doc
    </button>
</div>


<div class="quoteblock">
<blockquote>
<div class="paragraph">
<p>Amazon Simple Email Service (Amazon SES) is a cloud-based email sending service designed to help digital marketers and application developers send marketing, notification, and transactional emails. It is a reliable, cost-effective service for businesses of all sizes that use email to keep in contact with their customers.</p>
</div>
</blockquote>
</div>
<div class="paragraph">
<p>Add a dependency to AWS SES SDK:</p>
</div>
<div class="listingblock">
<div class="title">build.gradle</div>
<div class="content">
<pre class="highlightjs highlight"><code class="language-groovy" data-lang="groovy">    compile 'com.amazonaws:aws-java-sdk-ses:1.11.285'</code></pre>
</div>
</div>
<div class="paragraph">
<p>Create a service which encapsulates AWS credentials provider. The bean will not be loaded if the
environment variables (<code>AWS_ACCESS_KEY_ID</code>, <code>AWS_SECRET_KEY</code>) or system properties ( <code>aws.accesskeyid</code>, <code>aws.secretkey</code>) are not present.</p>
</div>
<div class="listingblock">
<div class="title">src/main/groovy/example/micronaut/AwsCredentialsProviderCondition.groovy</div>
<div class="content">
<pre class="highlightjs highlight"><code class="language-groovy" data-lang="groovy">package example.micronaut

import io.micronaut.context.condition.Condition
import io.micronaut.context.condition.ConditionContext

class AwsCredentialsProviderCondition implements Condition {
    @Override
    boolean matches(ConditionContext context) {
        (System.getProperty("aws.accesskeyid") || System.getenv("AWS_ACCESS_KEY_ID")) &amp;&amp;
                (System.getProperty("aws.secretkey") ||  System.getenv("AWS_SECRET_KEY"))
    }
}</code></pre>
</div>
</div>
<div class="paragraph">
<p>Add a test:</p>
</div>
<div class="listingblock">
<div class="title">src/test/groovy/example/micronaut/AwsCredentialsProviderConditionSpec.groovy</div>
<div class="content">
<pre class="highlightjs highlight"><code class="language-groovy" data-lang="groovy">package example.micronaut

import spock.lang.Specification
import spock.util.environment.RestoreSystemProperties

class AwsCredentialsProviderConditionSpec extends Specification {

    @RestoreSystemProperties
    def "condition is true if system properties are present"() {
        given:
        System.setProperty("aws.accesskeyid", "XXXX")
        System.setProperty("aws.secretkey", "YYYY")

        AwsCredentialsProviderCondition condition = new AwsCredentialsProviderCondition()

        expect:
        condition.matches(null)
    }

    def "condition is false if system properties are not present"() {
        given:
        AwsCredentialsProviderCondition condition = new AwsCredentialsProviderCondition()

        expect:
        !condition.matches(null)
    }
}</code></pre>
</div>
</div>
<div class="listingblock">
<div class="title">src/main/groovy/example/micronaut/AwsCredentialsProviderService.groovy</div>
<div class="content">
<pre class="highlightjs highlight"><code class="language-groovy" data-lang="groovy">package example.micronaut

import com.amazonaws.auth.AWSCredentials
import com.amazonaws.auth.AWSCredentialsProvider
import com.amazonaws.auth.BasicAWSCredentials
import groovy.transform.CompileStatic
import io.micronaut.context.annotation.Requires
import io.micronaut.context.annotation.Value
import javax.inject.Singleton

@Singleton <i class="conum" data-value="1"></i><b>(1)</b>
@Requires(condition = AwsCredentialsProviderCondition) <i class="conum" data-value="2"></i><b>(2)</b>
@CompileStatic
class AwsCredentialsProviderService implements AWSCredentialsProvider {

    String accessKey
    String secretKey

    AwsCredentialsProviderService(@Value('${AWS_ACCESS_KEY_ID:none}') String accessKeyEnv, <i class="conum" data-value="3"></i><b>(3)</b>
                                  @Value('${AWS_SECRET_KEY:none}') String secretKeyEnv,
                                  @Value('${aws.accesskeyid:none}') String accessKeyProp,
                                  @Value('${aws.secretkey:none}') String secretKeyProp) {
        this.accessKey = accessKeyEnv != null &amp;&amp; !accessKeyEnv.equals("none") ? accessKeyEnv : accessKeyProp;
        this.secretKey = secretKeyEnv != null &amp;&amp; !secretKeyEnv.equals("none")  ? accessKeyEnv : secretKeyProp;
    }

    @Override
    AWSCredentials getCredentials() {
        return new BasicAWSCredentials(accessKey, secretKey)
    }

    @Override
    void refresh() {

    }
}</code></pre>
</div>
</div>
<div class="colist arabic">
<table>
<tr>
<td><i class="conum" data-value="1"></i><b>1</b></td>
<td>Use <code>javax.inject.Singleton</code> to designate a class a a singleton</td>
</tr>
<tr>
<td><i class="conum" data-value="2"></i><b>2</b></td>
<td>Bean will not loaded unless condition is met.</td>
</tr>
<tr>
<td><i class="conum" data-value="3"></i><b>3</b></td>
<td>Values will be resolved from system properties.</td>
</tr>
</table>
</div>
<div class="paragraph">
<p>Add a test:</p>
</div>
<div class="listingblock">
<div class="title">src/test/groovy/example/micronaut/AwsCredentialsProviderServiceSpec.groovy</div>
<div class="content">
<pre class="highlightjs highlight"><code class="language-groovy" data-lang="groovy">package example.micronaut

import io.micronaut.context.ApplicationContext
import io.micronaut.context.exceptions.NoSuchBeanException
import io.micronaut.runtime.server.EmbeddedServer
import spock.lang.Specification
import spock.util.environment.RestoreSystemProperties

class AwsCredentialsProviderServiceSpec extends Specification {

    def "AwsCredentialsProviderService is not loaded if system property is not present"() {
        given:
        EmbeddedServer embeddedServer = ApplicationContext.run(EmbeddedServer)

        when:
        embeddedServer.applicationContext.getBean(AwsCredentialsProviderService)

        then:
        thrown(NoSuchBeanException)

        cleanup:
        embeddedServer.close()
    }

    @RestoreSystemProperties
    def "AwsCredentialsProviderService is loaded if system property is not present"() {
        given:
        System.setProperty("aws.accesskeyid", "XXXX")
        System.setProperty("aws.secretkey", "YYYY")
        EmbeddedServer embeddedServer = ApplicationContext.run(EmbeddedServer)

        when:
        AwsCredentialsProviderService bean = embeddedServer.applicationContext.getBean(AwsCredentialsProviderService)

        then:
        noExceptionThrown()
        bean.secretKey == 'YYYY'
        bean.accessKey == 'XXXX'

        cleanup:
        embeddedServer.close()
    }
}</code></pre>
</div>
</div>
<div class="paragraph">
<p>Create a service which sends the email with AWS Simple Email Service. The bean will not be loaded if the
environment variables (<code>AWS_REGION</code>, <code>AWS_SOURCE_EMAIL</code>) or system properties (<code>aws.region</code>, <code>aws.sourceemail</code>) are not present.</p>
</div>
<div class="listingblock">
<div class="title">src/main/groovy/example/micronaut/AwsSesMailCondition.groovy</div>
<div class="content">
<pre class="highlightjs highlight"><code class="language-groovy" data-lang="groovy">package example.micronaut

import groovy.transform.CompileStatic
import io.micronaut.context.condition.Condition
import io.micronaut.context.condition.ConditionContext

@CompileStatic
class AwsSesMailCondition implements Condition {

    @Override
    boolean matches(ConditionContext context) {
        (System.getProperty("aws.sourceemail") || System.getenv("AWS_SOURCE_EMAIL")) &amp;&amp;
                (System.getProperty("aws.region") ||  System.getenv("AWS_REGION"))
    }
}</code></pre>
</div>
</div>
<div class="paragraph">
<p>Add a test:</p>
</div>
<div class="listingblock">
<div class="title">src/test/groovy/example/micronaut/AwsSesMailConditionSpec.groovy</div>
<div class="content">
<pre class="highlightjs highlight"><code class="language-groovy" data-lang="groovy">package example.micronaut

import spock.lang.Specification
import spock.util.environment.RestoreSystemProperties

class AwsSesMailConditionSpec extends Specification {

    @RestoreSystemProperties
    def "condition is true if system properties are present"() {
        given:
        System.setProperty("aws.region", "XXXX")
        System.setProperty("aws.sourceemail", "me@micronaut.example")
        AwsSesMailCondition condition = new AwsSesMailCondition()

        expect:
        condition.matches(null)
    }

    def "condition is false if system properties are not present"() {
        given:
        AwsSesMailCondition condition = new AwsSesMailCondition()

        expect:
        !condition.matches(null)
    }
}</code></pre>
</div>
</div>
<div class="listingblock">
<div class="title">src/main/groovy/example/micronaut/AwsSesMailService.groovy</div>
<div class="content">
<pre class="highlightjs highlight"><code class="language-groovy" data-lang="groovy">package example.micronaut
import com.amazonaws.services.simpleemail.model.SendEmailResult
import groovy.transform.CompileStatic
import groovy.util.logging.Slf4j
import com.amazonaws.services.simpleemail.AmazonSimpleEmailService
import com.amazonaws.services.simpleemail.AmazonSimpleEmailServiceClientBuilder
import com.amazonaws.services.simpleemail.model.Body
import com.amazonaws.services.simpleemail.model.Content
import com.amazonaws.services.simpleemail.model.Destination
import com.amazonaws.services.simpleemail.model.Message
import com.amazonaws.services.simpleemail.model.SendEmailRequest
import io.micronaut.context.annotation.Primary
import io.micronaut.context.annotation.Requires
import io.micronaut.context.annotation.Value
import javax.inject.Singleton


@Singleton <i class="conum" data-value="1"></i><b>(1)</b>
@Requires(beans = AwsCredentialsProviderService) <i class="conum" data-value="2"></i><b>(2)</b>
@Requires(condition = AwsSesMailCondition)  <i class="conum" data-value="3"></i><b>(3)</b>
@Primary <i class="conum" data-value="4"></i><b>(4)</b>
@Slf4j
@CompileStatic
class AwsSesMailService implements EmailService {

    String awsRegion

    String sourceEmail

    AwsCredentialsProviderService awsCredentialsProviderService

    AwsSesMailService(@Value('${AWS_REGION:none}') String awsRegionEnv, <i class="conum" data-value="5"></i><b>(5)</b>
                      @Value('${AWS_SOURCE_EMAIL:none}') String sourceEmailEnv,
                      @Value('${aws.region:none}') String awsRegionProp,
                      @Value('${aws.sourceemail:none}') String sourceEmailProp,
                      AwsCredentialsProviderService awsCredentialsProviderService
    ) {
        this.awsRegion = awsRegionEnv != null &amp;&amp; !awsRegionEnv.equals("none") ? awsRegionEnv : awsRegionProp
        this.sourceEmail = sourceEmailEnv != null &amp;&amp; !sourceEmailEnv.equals("none") ? sourceEmailEnv : sourceEmailProp
        this.awsCredentialsProviderService = awsCredentialsProviderService
    }

    private Body bodyOfEmail(Email email) {
        if (email.htmlBody) {
            Content htmlBody = new Content().withData(email.htmlBody)
            return new Body().withHtml(htmlBody)
        }
        if (email.textBody) {
            Content textBody = new Content().withData(email.textBody)
            return new Body().withHtml(textBody)
        }
        new Body()
    }

    @Override
    void send(Email email) {

        if ( !awsCredentialsProviderService ) {
            log.warn("AWS Credentials provider not configured")
            return
        }

        Destination destination = new Destination().withToAddresses(email.recipient)
        if ( email.getCc() ) {
            destination = destination.withCcAddresses(email.getCc())
        }
        if ( email.getBcc() ) {
            destination = destination.withBccAddresses(email.getBcc())
        }
        Content subject = new Content().withData(email.getSubject())
        Body body = bodyOfEmail(email)
        Message message = new Message().withSubject(subject).withBody(body)

        SendEmailRequest request = new SendEmailRequest()
                .withSource(sourceEmail)
                .withDestination(destination)
                .withMessage(message)

        if ( email.getReplyTo() ) {
            request = request.withReplyToAddresses()
        }

        try {
            log.info("Attempting to send an email through Amazon SES by using the AWS SDK for Java...")

            AmazonSimpleEmailService client = AmazonSimpleEmailServiceClientBuilder.standard()
                    .withCredentials(awsCredentialsProviderService)
                    .withRegion(awsRegion)
                    .build()

            SendEmailResult sendEmailResult = client.sendEmail(request)
            log.info("Email sent! {}", sendEmailResult.toString())

        } catch (Exception ex) {
            log.warn("The email was not sent.")
            log.warn("Error message: {}", ex.message)
        }
    }
}</code></pre>
</div>
</div>
<div class="colist arabic">
<table>
<tr>
<td><i class="conum" data-value="1"></i><b>1</b></td>
<td>Use <code>javax.inject.Singleton</code> to designate a class a a singleton</td>
</tr>
<tr>
<td><i class="conum" data-value="2"></i><b>2</b></td>
<td>Bean will not loaded unless condition is met.</td>
</tr>
<tr>
<td><i class="conum" data-value="3"></i><b>3</b></td>
<td>Bean will not loaded, if bean is not present.</td>
</tr>
<tr>
<td><i class="conum" data-value="4"></i><b>4</b></td>
<td><code>@Primary</code> is a qualifier that indicates that a bean is the primary bean that should be selected in the case of multiple possible interface implementations.</td>
</tr>
<tr>
<td><i class="conum" data-value="5"></i><b>5</b></td>
<td>Values will be resolved from system properties.</td>
</tr>
</table>
</div>
<div class="paragraph">
<p>Add a test:</p>
</div>
<div class="listingblock">
<div class="title">src/test/groovy/example/micronaut/AwsSesMailServiceSpec.groovy</div>
<div class="content">
<pre class="highlightjs highlight"><code class="language-groovy" data-lang="groovy">package example.micronaut

import io.micronaut.context.ApplicationContext
import io.micronaut.context.exceptions.NoSuchBeanException
import io.micronaut.runtime.server.EmbeddedServer
import spock.lang.Specification
import spock.util.environment.RestoreSystemProperties

class AwsSesMailServiceSpec extends Specification {

    def "AwsSesMailService is not loaded if system property is not present"() {
        given:
        EmbeddedServer embeddedServer = ApplicationContext.run(EmbeddedServer)

        when:
        embeddedServer.applicationContext.getBean(AwsSesMailService)

        then:
        thrown(NoSuchBeanException)

        cleanup:
        embeddedServer.close()
    }

    @RestoreSystemProperties
    def "AwsSesMailService is loaded if system property is not present"() {
        given:
        System.setProperty("aws.region", "XXXX")
        System.setProperty("aws.sourceemail", "me@micronaut.example")
        System.setProperty("aws.accesskeyid", "XXXX")
        System.setProperty("aws.secretkey", "YYYY")
        EmbeddedServer embeddedServer = ApplicationContext.run(EmbeddedServer)

        when:
        embeddedServer.applicationContext.getBean(AwsCredentialsProviderService)
        AwsSesMailService bean = embeddedServer.applicationContext.getBean(AwsSesMailService)

        then:
        noExceptionThrown()
        bean.sourceEmail == 'me@micronaut.example'
        bean.awsRegion == 'XXXX'

        cleanup:
        embeddedServer.close()
    }
}</code></pre>
</div>
</div>


<h2 id="sendGridService">3.2.2 SendGrid</h2>

<div class='contribute-btn'>
    <button type='button' class='btn btn-default' onclick='window.location.href="https://github.com/micronaut-guides/micronaut-email-groovy/edit/master/src/main/docs/guide/writingTheApp/services/sendGridService.adoc"'>
        <i class='fa fa-pencil-square-o'></i> Improve this doc
    </button>
</div>


<div class="paragraph">
<p><a href="http://sendgrid.com">SendGrid</a> is a transactional email service.</p>
</div>
<div class="quoteblock">
<blockquote>
<div class="paragraph">
<p>SendGrid is responsible for sending billions of emails for some of the best and brightest companies in the world.</p>
</div>
</blockquote>
</div>
<div class="paragraph">
<p>Add a dependency to SendGrid SDK:</p>
</div>
<div class="listingblock">
<div class="title">build.gradle</div>
<div class="content">
<pre class="highlightjs highlight"><code class="language-groovy" data-lang="groovy">    compile 'com.sendgrid:sendgrid-java:4.1.2'</code></pre>
</div>
</div>
<div class="paragraph">
<p>Create a service which encapsulates the integration with SendGrid. The bean will not be loaded if the
system properties (<code>sendgrid.apikey</code>, <code>sendgrid.fromemail</code>) or environment variables (<code>SENDGRID_APIKEY</code>, <code>SENDGRID_FROM_EMAIL</code>) are not present.</p>
</div>
<div class="listingblock">
<div class="title">src/main/groovy/example/micronaut/SendGridEmailCondition.groovy</div>
<div class="content">
<pre class="highlightjs highlight"><code class="language-groovy" data-lang="groovy">package example.micronaut

import io.micronaut.context.condition.Condition
import io.micronaut.context.condition.ConditionContext

class SendGridEmailCondition implements Condition {

    @Override
    boolean matches(ConditionContext context) {
        (System.getProperty("sendgrid.apikey") || System.getenv("SENDGRID_APIKEY")) &amp;&amp;
                (System.getProperty("sendgrid.fromemail") ||  System.getenv("SENDGRID_FROM_EMAIL"))
    }
}</code></pre>
</div>
</div>
<div class="paragraph">
<p>Add a test:</p>
</div>
<div class="listingblock">
<div class="title">src/test/groovy/example/micronaut/SendGridEmailConditionSpec.groovy</div>
<div class="content">
<pre class="highlightjs highlight"><code class="language-groovy" data-lang="groovy">package example.micronaut

import spock.lang.Specification
import spock.util.environment.RestoreSystemProperties

class SendGridEmailConditionSpec extends Specification {

    @RestoreSystemProperties
    def "condition is true if system properties are present"() {
        given:
        System.setProperty("sendgrid.apikey", "XXXX")
        System.setProperty("sendgrid.fromemail", "me@micronaut.example")
        SendGridEmailCondition condition = new SendGridEmailCondition()

        expect:
        condition.matches(null)
    }

    def "condition is false if system properties are not present"() {
        given:
        SendGridEmailCondition condition = new SendGridEmailCondition()

        expect:
        !condition.matches(null)
    }
}</code></pre>
</div>
</div>
<div class="listingblock">
<div class="title">src/main/groovy/example/micronaut/SendGridEmailService.groovy</div>
<div class="content">
<pre class="highlightjs highlight"><code class="language-groovy" data-lang="groovy">package example.micronaut
import com.sendgrid.Personalization
import com.sendgrid.Content
import com.sendgrid.Mail
import com.sendgrid.SendGrid
import com.sendgrid.Request
import com.sendgrid.Response
import com.sendgrid.Method
import groovy.transform.CompileStatic
import groovy.util.logging.Slf4j
import io.micronaut.context.annotation.Requires
import io.micronaut.context.annotation.Value
import javax.inject.Singleton

@Singleton <i class="conum" data-value="1"></i><b>(1)</b>
@Requires(condition = SendGridEmailCondition) <i class="conum" data-value="2"></i><b>(2)</b>
@Slf4j
@CompileStatic
class SendGridEmailService implements EmailService {

    String apiKey

    String fromEmail

    SendGridEmailService(@Value('${SENDGRID_APIKEY:none}') String apiKeyEnv, <i class="conum" data-value="3"></i><b>(3)</b>
                         @Value('${SENDGRID_FROM_EMAIL:none}') String fromEmailEnv,
                         @Value('${sendgrid.apikey:none}') String apiKeyProp,
                         @Value('${sendgrid.fromemail:none}') String fromEmailProp) {
        this.apiKey = apiKeyEnv != null &amp;&amp; !apiKeyEnv.equals("none") ? apiKeyEnv : apiKeyProp;
        this.fromEmail = fromEmailEnv != null &amp;&amp; !fromEmailEnv.equals("none")  ? fromEmailEnv: fromEmailProp;
    }


    protected Content contentOfEmail(Email email) {
        if ( email.textBody ) {
            return new Content("text/plain", email.textBody)
        }
        if ( email.htmlBody ) {
            return new Content("text/html", email.htmlBody)
        }
        return null
    }

    @Override
    void send(Email email) {

        Personalization personalization = new Personalization()
        personalization.subject = email.subject

        com.sendgrid.Email to = new com.sendgrid.Email(email.recipient)
        personalization.addTo(to)

        if ( email.getCc() ) {
            for ( String cc : email.getCc() ) {
                com.sendgrid.Email ccEmail = new com.sendgrid.Email()
                ccEmail.email = cc
                personalization.addCc(ccEmail)
            }
        }

        if ( email.getBcc() ) {
            for ( String bcc : email.getBcc() ) {
                com.sendgrid.Email bccEmail = new com.sendgrid.Email()
                bccEmail.email = bcc
                personalization.addBcc(bccEmail)
            }
        }

        Mail mail = new Mail()
        com.sendgrid.Email from = new com.sendgrid.Email()
        from.email = fromEmail
        mail.from = from
        mail.addPersonalization(personalization)
        Content content = contentOfEmail(email)
        mail.addContent(content)

        SendGrid sg = new SendGrid(apiKey)
        Request request = new Request()
        try {
            request.with {
                method = Method.POST
                endpoint = "mail/send"
                body = mail.build()
            }
            Response response = sg.api(request)
            log.info("Status Code: {}", String.valueOf(response.getStatusCode()))
            log.info("Body: {}", response.getBody())
            if ( log.infoEnabled ) {
                response.getHeaders().each { String k, String v -&gt;
                    log.info("Response Header {} =&gt; {}", k, v)
                }
            }

        } catch (IOException ex) {
            log.error(ex.getMessage())
        }
    }
}</code></pre>
</div>
</div>
<div class="colist arabic">
<table>
<tr>
<td><i class="conum" data-value="1"></i><b>1</b></td>
<td>Use <code>javax.inject.Singleton</code> to designate a class a a singleton</td>
</tr>
<tr>
<td><i class="conum" data-value="2"></i><b>2</b></td>
<td>Bean will not loaded unless condition is met.</td>
</tr>
<tr>
<td><i class="conum" data-value="3"></i><b>3</b></td>
<td>Values will be resolved from system properties.</td>
</tr>
</table>
</div>
<div class="paragraph">
<p>Add a test:</p>
</div>
<div class="listingblock">
<div class="title">src/test/groovy/example/micronaut/SendGridEmailServiceSpec.groovy</div>
<div class="content">
<pre class="highlightjs highlight"><code class="language-groovy" data-lang="groovy">package example.micronaut

import io.micronaut.context.ApplicationContext
import io.micronaut.context.exceptions.NoSuchBeanException
import io.micronaut.runtime.server.EmbeddedServer
import spock.lang.Specification
import spock.util.environment.RestoreSystemProperties

class SendGridEmailServiceSpec extends Specification {

    def "SendGridEmailService is not loaded if system property is not present"() {
        given:
        EmbeddedServer embeddedServer = ApplicationContext.run(EmbeddedServer)

        when:
        embeddedServer.applicationContext.getBean(SendGridEmailService)

        then:
        thrown(NoSuchBeanException)

        cleanup:
        embeddedServer.close()
    }

    @RestoreSystemProperties
    def "SendGridEmailService is loaded if system property is not present"() {
        given:
        System.setProperty("sendgrid.apikey", "XXXX")
        System.setProperty("sendgrid.fromemail", "me@micronaut.example")
        EmbeddedServer embeddedServer = ApplicationContext.run(EmbeddedServer)

        when:
        SendGridEmailService bean = embeddedServer.applicationContext.getBean(SendGridEmailService)

        then:
        noExceptionThrown()
        bean.apiKey == 'XXXX'
        bean.fromEmail == 'me@micronaut.example'

        cleanup:
        embeddedServer.close()
    }
}</code></pre>
</div>
</div>


<h2 id="resources">3.3 Run the app</h2>

<div class='contribute-btn'>
    <button type='button' class='btn btn-default' onclick='window.location.href="https://github.com/micronaut-guides/micronaut-email-groovy/edit/master/src/main/docs/guide/writingTheApp/resources.adoc"'>
        <i class='fa fa-pencil-square-o'></i> Improve this doc
    </button>
</div>


<div class="paragraph">
<p>Add a logger to get more visibility:</p>
</div>
<div class="listingblock">
<div class="title">src/main/resources/logback.xml</div>
<div class="content">
<pre class="highlightjs highlight"><code class="language-groovy" data-lang="groovy">...
..
.
&lt;configuration&gt;

    &lt;appender name="STDOUT" class="ch.qos.logback.core.ConsoleAppender"&gt;
        &lt;!-- encoders are assigned the type
             ch.qos.logback.classic.encoder.PatternLayoutEncoder by default --&gt;
        &lt;encoder&gt;
            &lt;pattern&gt;%d{HH:mm:ss.SSS} [%thread] %-5level %logger{36} - %msg%n&lt;/pattern&gt;
        &lt;/encoder&gt;
    &lt;/appender&gt;

    &lt;root level="info"&gt;
        &lt;appender-ref ref="STDOUT" /&gt;
    &lt;/root&gt;

    &lt;logger name="example.micronaut" level="TRACE"/&gt;

&lt;/configuration&gt;</code></pre>
</div>
</div>
<div class="paragraph">
<p>To use SendGrid, setup the required environment variables and run the app</p>
</div>
<div class="listingblock">
<div class="content">
<pre class="highlightjs highlight"><code class="language-bash" data-lang="bash">$ export SENDGRID_FROM_EMAIL=email@email.com
$ export SENDGRID_APIKEY=XXXXXX
$ ./gradlew run</code></pre>
</div>
</div>
<div class="paragraph">
<p>To use AWS SES, setup the required environment variables and run the app</p>
</div>
<div class="listingblock">
<div class="content">
<pre class="highlightjs highlight"><code class="language-bash" data-lang="bash">$ export AWS_REGION=eu-west-1
$ export AWS_SOURCE_EMAIL=email@email.com
$ export AWS_ACCESS_KEY_ID=XXXXXXXX
$ export AWS_SECRET_KEY=XXXXXXXX
$ ./gradlew run</code></pre>
</div>
</div>
<div class="paragraph">
<p>If you supply both AWS SES and SendGrid environment variables, the AWS SES <code>EmailService</code> implementation will be used due to the <code>@Primary</code> annotation.</p>
</div>


<h2 id="test">3.4 Test</h2>

<div class='contribute-btn'>
    <button type='button' class='btn btn-default' onclick='window.location.href="https://github.com/micronaut-guides/micronaut-email-groovy/edit/master/src/main/docs/guide/writingTheApp/test.adoc"'>
        <i class='fa fa-pencil-square-o'></i> Improve this doc
    </button>
</div>


<div class="paragraph">
<p>In our acceptance test, beans <code>SendGridEmailService</code> or <code>AwsSesMailService</code> will not be loaded since system properties are not present.</p>
</div>
<div class="paragraph">
<p>Instead, we setup a Mock which we can verify interactions against.</p>
</div>
<div class="listingblock">
<div class="title">src/test/groovy/example/micronaut/MockEmailService.groovy</div>
<div class="content">
<pre class="highlightjs highlight"><code class="language-groovy" data-lang="groovy">package example.micronaut

import io.micronaut.context.annotation.Primary
import io.micronaut.context.annotation.Requires

import javax.inject.Singleton

@Primary
@Requires(property = 'spec.name', value = 'mailcontroller')
@Singleton
class MockEmailService implements EmailService {

    List&lt;Email&gt; emails = []

    @Override
    void send(Email email) {
        emails &lt;&lt; email
    }
}</code></pre>
</div>
</div>
<div class="paragraph">
<p>Create the next test:</p>
</div>
<div class="listingblock">
<div class="title">src/test/groovy/example/micronaut/MailControllerSpec.groovy</div>
<div class="content">
<pre class="highlightjs highlight"><code class="language-groovy" data-lang="groovy">package example.micronaut

import io.micronaut.context.ApplicationContext
import io.micronaut.http.HttpRequest
import io.micronaut.http.HttpResponse
import io.micronaut.http.client.RxHttpClient
import io.micronaut.runtime.server.EmbeddedServer
import spock.lang.AutoCleanup
import spock.lang.Shared
import spock.lang.Specification

import javax.annotation.Nullable

class MailControllerSpec extends Specification {

    @Shared
    @AutoCleanup <i class="conum" data-value="1"></i><b>(1)</b>
    EmbeddedServer embeddedServer = ApplicationContext.run(EmbeddedServer, [
            'spec.name': 'mailcontroller',
    ], 'test')  <i class="conum" data-value="2"></i><b>(2)</b>

    @Shared
    @AutoCleanup
    RxHttpClient client = embeddedServer.applicationContext.createBean(RxHttpClient, embeddedServer.getURL()) <i class="conum" data-value="3"></i><b>(3)</b>

    def "/mail/send interacts once email service"() {
        given:
        EmailCmd cmd = new EmailCmd(subject: 'Test',
                recipient: 'delamos@grails.example',
                textBody: 'Hola hola')
        HttpRequest request = HttpRequest.POST('/mail/send', cmd) <i class="conum" data-value="4"></i><b>(4)</b>

        when:
        Collection emailServices = embeddedServer.applicationContext.getBeansOfType(EmailService)

        then:
        !emailServices.any { it == SendGridEmailService.class}
        !emailServices.any { it == AwsSesMailService.class}

        when:
        EmailService emailService = embeddedServer.applicationContext.getBean(EmailService)

        then:
        emailService instanceof MockEmailService

        when:
        HttpResponse rsp = client.toBlocking().exchange(request)

        then:
        rsp.status.code == 200
        ((MockEmailService)emailService).emails.size() == old(((MockEmailService)emailService).emails.size()) + 1 <i class="conum" data-value="5"></i><b>(5)</b>
    }
}</code></pre>
</div>
</div>
<div class="colist arabic">
<table>
<tr>
<td><i class="conum" data-value="1"></i><b>1</b></td>
<td>The AutoCleanup extension makes sure the <code>close()</code> method of an object (e.g. <code>EmbeddedServer</code>) is called each time a feature method is finished</td>
</tr>
<tr>
<td><i class="conum" data-value="2"></i><b>2</b></td>
<td>To run the application from a unit test you can use the <a href="http://docs.micronaut.io/snapshot/api/io/micronaut/runtime/server/EmbeddedServer.html">EmbeddedServer</a> interface</td>
</tr>
<tr>
<td><i class="conum" data-value="3"></i><b>3</b></td>
<td>Register a <code>HttpClient</code> bean in the application context and point it to the embedded server URL. The <code>EmbeddedServer</code> interface provides the URL of the server under test which runs on a random port.</td>
</tr>
<tr>
<td><i class="conum" data-value="4"></i><b>4</b></td>
<td>Creating HTTP Requests is easy thanks to Micronaut&#8217;s fluid API.</td>
</tr>
<tr>
<td><i class="conum" data-value="5"></i><b>5</b></td>
<td><code>emailService.send</code> method is invoked once.</td>
</tr>
</table>
</div>


<h2 id="validation">3.5 Validation</h2>

<div class='contribute-btn'>
    <button type='button' class='btn btn-default' onclick='window.location.href="https://github.com/micronaut-guides/micronaut-email-groovy/edit/master/src/main/docs/guide/writingTheApp/validation.adoc"'>
        <i class='fa fa-pencil-square-o'></i> Improve this doc
    </button>
</div>


<div class="paragraph">
<p>We want to ensure any email request contains a subject, recipient and a text body or html body.</p>
</div>
<div class="paragraph">
<p>Micronaut&#8217;s validation is built on with the standard framework â€“ JSR 380, also known as Bean Validation 2.0.</p>
</div>
<div class="paragraph">
<p>Hibernate Validator is a reference implementation of the validation API.</p>
</div>
<div class="paragraph">
<p>Add a dependency to it:</p>
</div>
<div class="listingblock">
<div class="title">build.gradle</div>
<div class="content">
<pre class="highlightjs highlight"><code class="language-java" data-lang="java">    compile "io.micronaut:micronaut-validation"
    compile "io.micronaut.configuration:micronaut-hibernate-validator"</code></pre>
</div>
</div>
<div class="paragraph">
<p>Create the next test:</p>
</div>
<div class="listingblock">
<div class="title">src/test/java/example/micronaut/MailControllerValidationSpec.groovy</div>
<div class="content">
<pre class="highlightjs highlight"><code class="language-java" data-lang="java">package example.micronaut

import io.micronaut.context.ApplicationContext
import io.micronaut.http.HttpRequest
import io.micronaut.http.HttpResponse
import io.micronaut.http.client.RxHttpClient
import io.micronaut.http.client.exceptions.HttpClientResponseException
import io.micronaut.runtime.server.EmbeddedServer
import spock.lang.AutoCleanup
import spock.lang.Shared
import spock.lang.Specification


class MailControllerValidationSpec extends Specification {

    @Shared
    @AutoCleanup
    <i class="conum" data-value="1"></i><b>(1)</b>
    EmbeddedServer embeddedServer = ApplicationContext.run(EmbeddedServer, [
            'spec.name': 'mailcontroller',
    ], 'test')  <i class="conum" data-value="2"></i><b>(2)</b>

    @Shared
    @AutoCleanup
    RxHttpClient client = embeddedServer.applicationContext.createBean(RxHttpClient, embeddedServer.getURL()) <i class="conum" data-value="3"></i><b>(3)</b>


    def "/mail/send cannot be invoked without subject"() {
        given:
        EmailCmd cmd = new EmailCmd(
                recipient: 'delamos@micronaut.example',
                textBody: 'Hola hola')
        HttpRequest request = HttpRequest.POST('/mail/send', cmd) <i class="conum" data-value="4"></i><b>(4)</b>

        when:
        client.toBlocking().exchange(request)

        then:
        HttpClientResponseException e = thrown(HttpClientResponseException)
        e.status.code == 400
    }

    def "/mail/send cannot be invoked without recipient"() {
        given:
        EmailCmd cmd = new EmailCmd(
                subject: 'Hola',
                textBody: 'Hola hola')
        HttpRequest request = HttpRequest.POST('/mail/send', cmd) <i class="conum" data-value="4"></i><b>(4)</b>

        when:
        client.toBlocking().exchange(request)

        then:
        HttpClientResponseException e = thrown(HttpClientResponseException)
        e.status.code == 400
    }

    def "/mail/send cannot be invoked without either textBody or htmlBody"() {
        given:
        EmailCmd cmd = new EmailCmd(
                subject: 'Hola',
                recipient: 'delamos@micronaut.example',)
        HttpRequest request = HttpRequest.POST('/mail/send', cmd) <i class="conum" data-value="4"></i><b>(4)</b>

        when:
        client.toBlocking().exchange(request)

        then:
        HttpClientResponseException e = thrown(HttpClientResponseException)
        e.status.code == 400
    }

    def "/mail/send can be invoked without textBody and not htmlBody"() {
        given:
        EmailCmd cmd = new EmailCmd(
                subject: 'Hola',
                recipient: 'delamos@micronaut.example',
                textBody: 'Hello')
        HttpRequest request = HttpRequest.POST('/mail/send', cmd) <i class="conum" data-value="4"></i><b>(4)</b>

        when:
        HttpResponse rsp = client.toBlocking().exchange(request)

        then:
        rsp.status().code == 200
    }

    def "/mail/send can be invoked without htmlBody and not textBody"() {
        given:
        EmailCmd cmd = new EmailCmd(
                subject: 'Hola',
                recipient: 'delamos@micronaut.example',
                htmlBody: '&lt;h1&gt;Hello&lt;/h1&gt;')
        HttpRequest request = HttpRequest.POST('/mail/send', cmd) <i class="conum" data-value="4"></i><b>(4)</b>

        when:
        HttpResponse rsp = client.toBlocking().exchange(request)

        then:
        rsp.status().code == 200
    }
}</code></pre>
</div>
</div>
<div class="colist arabic">
<table>
<tr>
<td><i class="conum" data-value="1"></i><b>1</b></td>
<td>The AutoCleanup extension makes sure the <code>close()</code> method of an object (e.g. <code>EmbeddedServer</code>) is called each time a feature method is finished</td>
</tr>
<tr>
<td><i class="conum" data-value="2"></i><b>2</b></td>
<td>To run the application from a unit test you can use the <a href="http://docs.micronaut.io/snapshot/api/io/micronaut/runtime/server/EmbeddedServer.html">EmbeddedServer</a> interface</td>
</tr>
<tr>
<td><i class="conum" data-value="3"></i><b>3</b></td>
<td>Register a <code>HttpClient</code> bean in the application context and point it to the embedded server URL. The <code>EmbeddedServer</code> interface provides the URL of the server under test which runs on a random port.</td>
</tr>
<tr>
<td><i class="conum" data-value="4"></i><b>4</b></td>
<td>Creating HTTP Requests is easy thanks to Micronaut&#8217;s fluid API.</td>
</tr>
</table>
</div>
<div class="paragraph">
<p>In order to satisfy the test, create an email constraints annotation</p>
</div>
<div class="listingblock">
<div class="title">src/main/groovy/example/micronaut/EmailConstraints.java</div>
<div class="content">
<pre class="highlightjs highlight"><code class="language-java" data-lang="java">package example.micronaut;

import groovy.transform.CompileStatic;

import javax.validation.Constraint;
import javax.validation.Payload;
import java.lang.annotation.ElementType;
import java.lang.annotation.Retention;
import java.lang.annotation.RetentionPolicy;
import java.lang.annotation.Target;

@Constraint(validatedBy = { EmailConstraintsValidator.class })
@Target( { ElementType.TYPE })
@Retention(RetentionPolicy.RUNTIME)
@CompileStatic
@interface EmailConstraints {

    String message() default "{email.invalid}";
    Class&lt;?&gt;[] groups() default { };
    Class&lt;? extends Payload&gt;[] payload() default { };
}</code></pre>
</div>
</div>
<div class="paragraph">
<p>and a validator:</p>
</div>
<div class="listingblock">
<div class="title">src/main/groovy/example/micronaut/EmailConstraintsValidator.groovy</div>
<div class="content">
<pre class="highlightjs highlight"><code class="language-groovy" data-lang="groovy">package example.micronaut

import groovy.transform.CompileStatic

import javax.validation.ConstraintValidator
import javax.validation.ConstraintValidatorContext

@CompileStatic
class EmailConstraintsValidator implements ConstraintValidator&lt;EmailConstraints, EmailCmd&gt; {

    @Override
    boolean isValid(EmailCmd email, ConstraintValidatorContext context) {
        email &amp;&amp; (email.textBody || email.htmlBody)
    }
}</code></pre>
</div>
</div>
<div class="paragraph">
<p>Annotate Email with <code>EmailConstraints</code> and populate the <code>hasErrors</code> method.</p>
</div>
<div class="listingblock">
<div class="title">src/main/java/example/micronaut/EmailCmd.groovy</div>
<div class="content">
<pre class="highlightjs highlight"><code class="language-java" data-lang="java">import groovy.transform.CompileStatic
import groovy.transform.ToString
import javax.validation.Validation
import javax.validation.constraints.NotBlank
import javax.validation.constraints.NotNull

@ToString
@CompileStatic
@EmailConstraints <i class="conum" data-value="1"></i><b>(1)</b>
class EmailCmd implements Email {

    @NotNull
    @NotBlank
    String recipient

    @NotNull
    @NotBlank
    String subject

    List&lt;String&gt; cc = []
    List&lt;String&gt; bcc = []
    String htmlBody
    String textBody
    String replyTo
}</code></pre>
</div>
</div>


<h1 id="testingTheApp">4 Testing the Application</h1>

<div class='contribute-btn'>
    <button type='button' class='btn btn-default' onclick='window.location.href="https://github.com/micronaut-guides/micronaut-email-groovy/edit/master/src/main/docs/guide/testingTheApp.adoc"'>
        <i class='fa fa-pencil-square-o'></i> Improve this doc
    </button>
</div>


<div class="paragraph">
<p>To run the tests:</p>
</div>
<div class="listingblock">
<div class="content">
<pre class="highlightjs highlight"><code class="language-bash" data-lang="bash">$ ./gradlew test
$ open build/reports/tests/test/index.html</code></pre>
</div>
</div>

</article>
</div>
<!-- Global site tag (gtag.js) - Google Analytics -->
<script async src="https://www.googletagmanager.com/gtag/js?id=UA-115754405-1"></script>
<script>
  window.dataLayer = window.dataLayer || [];
  function gtag(){dataLayer.push(arguments);}
  gtag('js', new Date());
  gtag('config', 'UA-115754405-1');
</script>
</body>
</html>
